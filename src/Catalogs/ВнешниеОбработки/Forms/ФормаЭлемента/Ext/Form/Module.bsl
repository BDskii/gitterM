
&НаКлиенте
Процедура Загрузить(Команда)
	
	Если не ЗначениеЗаполнено(Объект.ВидОбработки) Тогда 
		Сообщить("Не выбран вид обработки.");
		Возврат;
	КонецЕсли;	
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Этап","Начало");
	ДополнительныеПараметры.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ДополнительныеПараметры.Вставить("ОбработкаЗавершения", Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтаФорма));
	ДополнительныеПараметры.Вставить("ОписаниеДиалога", Новый Структура);
	
	ОписаниеДиалогаПоВидуОбработки(Объект.ВидОбработки, ДополнительныеПараметры.ОписаниеДиалога);
	
	ОбщегоНазначенияКлиент.ЗагрузкаФайлаАсинхронно(Неопределено,ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ДополнительныеПараметрыСохранения = Новый Структура;
	ДополнительныеПараметрыСохранения.Вставить("Этап","Начало");
	ДополнительныеПараметрыСохранения.Вставить("ПолучаемыеФайлы",Новый Массив);
	ДополнительныеПараметрыСохранения.ПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(Объект.ИмяФайла, ПоместитьФайлВоВременноеХранилище()));
	ДополнительныеПараметрыСохранения.Вставить("ОписаниеДиалога",Новый Структура);	
	ОписаниеДиалогаПоВидуОбработки(Объект.ВидОбработки, ДополнительныеПараметрыСохранения.ОписаниеДиалога); 
	ОбщегоНазначенияКлиент.СохранениеФайловАсинхронно(Неопределено,ДополнительныеПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайла(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ОписаниеФайла = Результат[0];
	
	
	СтруктураимениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ОписаниеФайла.Имя);
	Объект.ИмяФайла = СтруктураимениФайла.Имя;
	
	Если не ЗначениеЗаполнено(Объект.Наименование) Тогда 
		Объект.Наименование = СтруктураимениФайла.ИмяБезРасширения;
	КонецЕсли;	
	
	СохранитьФайлВхранилище(ОписаниеФайла.Хранение);	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОписаниеДиалогаПоВидуОбработки(ВидОбработки, ОписаниеДиалога) 
	
	ДопДанные = Справочники.ВнешниеОбработки.ДополнительныеДанныеПоВидуОбработки(ВидОбработки);
	
	ОписаниеДиалога.Вставить("Заголовок", "Выберите файл");
	ОписаниеДиалога.Вставить("ПолноеИмяФайла", "");
	ОписаниеДиалога.Вставить("ПредварительныйПросмотр", Истина);
	ОписаниеДиалога.Вставить("МножественныйВыбор", Ложь);
	ОписаниеДиалога.Вставить("Фильтр", ДопДанные.Фильтр);
		
КонецПроцедуры	

&НаСервере
Процедура СохранитьФайлВхранилище(Адрес)
	 
	ДД = ПолучитьИзВременногоХранилища(Адрес);
	Объект.Хранилище = Base64Строка(ДД);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьФайлВоВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(Base64Значение(Объект.Хранилище),УникальныйИдентификатор);
	
КонецФункции	

