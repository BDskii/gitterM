
Процедура ЛогОтладка(пТекст) Экспорт
	
	ОбщегоНазначениеСервер.ЗаписатьИнформациюВЖурнал("ЛогОтладка", пТекст, "Информация");
	
	Сообщить( "" + ТекущаяДата() + ". " + пТекст, СтатусСообщения.БезСтатуса );
	
КонецПроцедуры

Процедура ЛогИнформация( Знач пТекст ) Экспорт
	
	ОбщегоНазначениеСервер.ЗаписатьИнформациюВЖурнал("ЛогИнформация", пТекст, "Информация");
	
	Сообщить( "" + ТекущаяДата() + ". " + пТекст, СтатусСообщения.Информация );
	
КонецПроцедуры

Процедура ЛогОшибка( Знач пТекст ) Экспорт
	
	ОбщегоНазначениеСервер.ЗаписатьИнформациюВЖурнал("ЛогОшибка", пТекст, "Ошибка");
	
	
	Сообщить( "" + ТекущаяДата() + ". " + пТекст, СтатусСообщения.ОченьВажное );
	
КонецПроцедуры

Процедура ЛогКоманда( Знач пТекст ) Экспорт
	
	ОбщегоНазначениеСервер.ЗаписатьИнформациюВЖурнал("ЛогКоманда", пТекст, "Информация");

	
	Сообщить( " > " + пТекст );
	
КонецПроцедуры

Процедура ЛогВыводКоманды( Знач пТекст ) Экспорт

	ОбщегоНазначениеСервер.ЗаписатьИнформациюВЖурнал("ЛогВыводКоманды", пТекст, "Информация");
	
	Сообщить( "		>> " + пТекст );

КонецПроцедуры

Процедура ЛогВыводКомандыИзФайла( имяФайлаВывода ) Экспорт

	чтениеФайла = Новый ЧтениеТекста( имяФайлаВывода, КодировкаТекста.UTF8 );
	
	текСтрока = чтениеФайла.ПрочитатьСтроку();
	
	Пока текСтрока <> Неопределено Цикл
		
		ЛогВыводКоманды( текСтрока );
		
		текСтрока = чтениеФайла.ПрочитатьСтроку();
		
	КонецЦикла;
	
	чтениеФайла.Закрыть();

	УдалитьФайлы( имяФайлаВывода );

КонецПроцедуры



Функция ФайлСуществует( Знач пИмяФайла ) Экспорт
	
	Если Не ЗначениеЗаполнено( пИмяФайла ) Тогда
		
		Возврат Ложь;
	
	КонецЕсли;
	
	Файл = Новый Файл( пИмяФайла );
	
	Возврат Файл.Существует() И Файл.ЭтоФайл();
	
КонецФункции

Процедура ОбеспечитьТекстовыйФайл( Знач пИмяФайла ) Экспорт
	
	Если Не ЗначениеЗаполнено( пИмяФайла ) Тогда
		
		Возврат;
	
	КонецЕсли;
	
	Если Не ФайлСуществует( пИмяФайла ) Тогда
		
		записьФайла = Новый ЗаписьТекста( пИмяФайла );
		записьФайла.Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбеспечитьКаталог( Знач Путь ) Экспорт
	
	Объект = Новый Файл( Путь );
	
	Если Не Объект.Существует() Тогда
		
		СоздатьКаталог( Путь );
		
	ИначеЕсли НЕ Объект.ЭтоКаталог() Тогда
		
		ВызватьИсключение "Не удается создать каталог " + Путь + ". По данному пути уже существует файл.";
		
	КонецЕсли;
	
КонецПроцедуры

Функция Экранировать( Значение ) Экспорт
	
	Возврат СтрЗаменить( Значение, """", """""" );
	
КонецФункции


#Если Не ВебКлиент Тогда

Функция ВыполнитьКоманду( Знач ЛокальныйРепозиторийАдрес,
						  Знач ТекстКоманды,
						  Знач пОжидатьЗавершения = Истина,
						  Знач пПропуститьВыводЛога = Ложь ) Экспорт
	
	Если Не пПропуститьВыводЛога Тогда
		
		файлВывода = ПолучитьИмяВременногоФайла( "txt" );
		
		ЛогКоманда( ТекстКоманды );
		
		ТекстКоманды = ТекстКоманды + " > " + Экранировать( файлВывода ) + " 2>&1";
		
	КонецЕсли;
	
	командныйФайл = ПолучитьИмяВременногоФайла( "bat" );
	
	записьТекста = Новый ЗаписьТекста( командныйФайл, "cp866" );
	записьТекста.ЗаписатьСтроку( ТекстКоманды );
	записьТекста.Закрыть();
	
	КодВозврата = ВыполнитьКомандныйФайл( ЛокальныйРепозиторийАдрес, командныйФайл, пОжидатьЗавершения );
	
	Попытка
		УдалитьФайлы( командныйФайл );
	Исключение
		ЛогОшибка("Не удалось удалить коммандный файл:"""+командныйФайл+"""."+Символы.ПС + ОписаниеОшибки());
	КонецПопытки;
	
	Если Не пПропуститьВыводЛога Тогда
		
		ЛогВыводКомандыИзФайла( файлВывода );
		
	КонецЕсли;
	
	Возврат КодВозврата;
	
КонецФункции

Функция ВыполнитьКомандныйФайл( Знач ЛокальныйРепозиторийАдрес,
								Знач пИмяФайла,
								Знач пОжидатьЗавершения = Истина ) Экспорт
	
	КодВозврата = Неопределено;
	
	запускальщик = _ОбщегоНазначенияПовтИсп.Запускальщик(); 
	
	Если запускальщик = Неопределено Тогда
		
		ЗапуститьПриложение( пИмяФайла, ЛокальныйРепозиторийАдрес, пОжидатьЗавершения, КодВозврата );
		
	Иначе
		
		запускальщик.CurrentDirectory = ЛокальныйРепозиторийАдрес;
		
		КодВозврата = запускальщик.Run( пИмяФайла, 0, пОжидатьЗавершения);
		
	КонецЕсли;
	
	Возврат КодВозврата;
	
КонецФункции


#КонецЕсли


